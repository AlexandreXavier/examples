<?xml version="1.0" encoding="utf-8"?>
<s:TitleWindow xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" width="400" height="300"
		 title="Authorize Request Token">
	
	<fx:Metadata>
		[Event(name="complete",type="flash.events.Event")]
	</fx:Metadata>
	
	<fx:Script>
		<![CDATA[
			import org.iotashan.oauth.OAuthToken;
			import org.iotashan.utils.URLEncoding;
			
			public var oauth_verifier:String;
			
			public function authorizeToken(authorizeTokenUrl:String, requestToken:OAuthToken):void
			{				
				var authUrl:String = authorizeTokenUrl + "?oauth_token=" + requestToken.key;
				html.location = authUrl;
				
				trace("Token auth url:", authUrl);
			}

			protected function html_locationChangeHandler(event:Event):void
			{
				if (html.location.indexOf("oauth_verifier") >= 0)
				{
					var urlSplit:Array = html.location.split("?");
					if (urlSplit.length == 2)
					{
						var params:Array = urlSplit[1].split("&");
						for each(var param:String in params)
						{
							var paramSplit:Array = param.split("=");
							if (paramSplit.length == 2 && paramSplit[0] == "oauth_verifier")
							{
								oauth_verifier = URLEncoding.decode(paramSplit[1]);
								dispatchEvent(new Event(Event.COMPLETE));
								
								break;
							}
						}
					}
				}
			}
		]]>
	</fx:Script>
	
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	
	<mx:HTML id="html" width="100%" height="100%" locationChange="html_locationChangeHandler(event)" />
	
</s:TitleWindow>
