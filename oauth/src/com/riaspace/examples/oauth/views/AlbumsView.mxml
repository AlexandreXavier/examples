<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" width="400" height="300">

	<fx:Script>
		<![CDATA[
			import mx.collections.XMLListCollection;
			import mx.controls.Alert;
			import mx.core.FlexGlobals;
			
			import org.iotashan.oauth.IOAuthSignatureMethod;
			import org.iotashan.oauth.OAuthConsumer;
			import org.iotashan.oauth.OAuthRequest;
			import org.iotashan.oauth.OAuthToken;
			
			public function loadAlbums():void
			{
				var accessToken:OAuthToken = FlexGlobals.topLevelApplication.accessToken;
				var consumer:OAuthConsumer = FlexGlobals.topLevelApplication.consumer;
				var signature:IOAuthSignatureMethod = FlexGlobals.topLevelApplication.signature;
				
				var oauthRequest:OAuthRequest = 
					new OAuthRequest(
						OAuthRequest.HTTP_MEHTOD_GET, "https://picasaweb.google.com/data/feed/api/user/default", 
						null, 
						consumer, accessToken
					);
				
				var request:URLRequest = new URLRequest(oauthRequest.requestURL);
				request.requestHeaders.push(oauthRequest.buildRequest(signature, OAuthRequest.RESULT_TYPE_HEADER));
				request.method = URLRequestMethod.GET;
				request.contentType = "application/atom+xml";
				
				var loader:URLLoader = new URLLoader;
				loader.addEventListener(Event.COMPLETE, getAlbums_completeHandler);
				loader.addEventListener(IOErrorEvent.IO_ERROR, getAlbums_ioErrorHandler);
				loader.load(request);
			}
			
			protected function getAlbums_completeHandler(event:Event):void
			{
				currentState = "loaded";
				
				default xml namespace = new Namespace("http://www.w3.org/2005/Atom");
				var contactsXml:XML = XML(URLLoader(event.currentTarget).data);
				
				lstAlbums.dataProvider = new XMLListCollection(contactsXml.entry.title);
			}
			
			protected function getAlbums_ioErrorHandler(event:IOErrorEvent):void
			{
				Alert.show("Error getting albums: " + URLLoader(event.currentTarget).data, "Error!");
			}
		]]>
	</fx:Script>
	
	<s:states>
		<s:State name="loading" />
		<s:State name="loaded" />
	</s:states>
	
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	
	<s:Label text="Loading your albums..." verticalCenter="0" horizontalCenter="0" includeIn="loading" />
	
	<s:List id="lstAlbums" labelField="title" borderVisible="false" width="100%" height="100%"
			itemRenderer="com.riaspace.examples.oauth.views.renderers.AlbumRenderer" includeIn="loaded">
		<s:layout>
			<s:TileLayout columnAlign="justifyUsingGap" verticalGap="20" />
		</s:layout>
	</s:List>

</s:Group>
